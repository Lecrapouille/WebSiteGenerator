dnl #################################################################
dnl M4 preferences
dnl #################################################################
changequote([,])
changecom
undefine([format])

define([DIVERT_FOOTER_CODE],[42])
define([DIVERT_BODY_CODE],[43])
define([DIVERT_NAVIGATION_CODE],[44])
define([DIVERT_NAVIGATION_CODE2],[45])
define([DIVERT_HEADER_CODE],[46])

dnl #################################################################
dnl # Macros utiles
dnl #################################################################
define([UPPER],[translit([$1],[a-z],[A-Z])])
define([LOWER],[translit([$1],[A-Z],[a-z])])
define([forloop], [pushdef([$1], [$2])_forloop([$1], [$2], [$3], [$4])popdef([$1])])
define([_forloop],[$4[]ifelse($1, [$3], ,[define([$1], incr($1))_forloop([$1], [$2], [$3], [$4])])])

dnl #####################################################################
dnl Langues
dnl #####################################################################
ifdef([LANG],,[define([LANG],[fr])])
define([CHANGE_LANG],[<p>ifelse(LANG,fr,[DRAP_FR
LINK(THISFILE-en.html,DRAP_EN)],[LINK(THISFILE-fr.html,DRAP_FR)
DRAP_EN])</p>])

define([CHANGE_LANGAGE],[ifelse(LANG,fr,
[<a href="$1.htm"><img src="img/fr.png" style="border:0;" alt="FR" /></a>],
[<img src="img/fr.png" style="border:0;" alt="FR" />])
ifelse(LANG,en,
[<a href="$1.htm"><img src="img/en.png" style="border:0;" alt="EN" /></a>],
[<img src="img/en.png" style="border:0;" alt="EN" />])])

dnl #####################################################################
dnl Mise a jour
dnl #####################################################################
define([DATE],esyscmd(date "+%a %d %b %y (%Hh%M)"))
define([SIMTADYN_ANNIVERSARY],[eval(esyscmd(date "+%y") - 03)])
define([PRINT_MAJ],[ifelse(LANG,fr,[Mise à jour le : ],[Last update: ])DATE])

dnl #####################################################################
dnl HTML Liens, images
dnl #####################################################################
define([LINK],[ifelse([$2],,[<a href="$1">$1</a>],[<a href="$1">$2</a>])])
define([MYLINK],[ifelse([$2],,[<a href="$1-LANG.html">$1</a>],[<a href="$1-LANG.html">$2</a>])])
define([EXTLINK],[<a href="$1" target="_blank">ifelse([$2],,[$1],[$2])
<img src="img/externallink.jpg" alt="ext link" border="0"/></a>])

define([MOVIE],[<a href="$2"><img class="caption" src="$1" alt="caption"/></a>])
define([CAPTION],[<a href="$1"><img class="caption" src="$1" alt="caption"/></a>])
define([LOWCAPTION],[<a href="$1_big.jpg"><img class="caption" src="$1.jpg" alt="caption"/></a>])
define([PICTURE],[CENTER(CAPTION($1)<p>$2</p>)])
define([LOWPICTURE],[CENTER(LOWCAPTION($1)<p>$2</p>)])

dnl #####################################################################
dnl HTML Meta et titre
dnl #####################################################################
define([DEF_KEYWORDS],[define([_META_KEYWORDS_],[$1])])
define([DEF_DESCRIPTION],[define([_META_DESCRIPTION_],[$1])])
define([DEF_TITLE],[define([_META_TITLE_],[$1])])
define([MENU_ACTIF],[define([_SSMENU_],[$1])])

dnl #####################################################################
dnl HTML structure
dnl #####################################################################
define([HTML_FOOTER],[</body> </html>])
define([_HTML_HEADER_],[<?xml version="1.0" encoding="iso-8859-1"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="]LANG[" lang="]LANG[">
<head>
<!-- Generated by script the DATE -->
<title>]_META_TITLE_[</title>
<meta name="keywords" content="]_META_KEYWORDS_[" />
<meta name="description" content="]_META_DESCRIPTION_[" />
<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
<link href=ifelse($1,,["css/styles.css"],["css/$1"]) rel="stylesheet" type="text/css" />
</head><body><a id="gotoup" name="gotoup"></a>])

dnl #####################################################################
dnl HTML Tableaux
dnl #####################################################################
define([VECT_HEAD],[$1[]define([_list],[shift($@)])])

define([TABLEAU],[
define([COL],[$1]) dnl nombre de colonnes
define([_list],[shift($@)])
define([LIG],[eval($# / COL)]) dnl nombre de lignes
<table width="90%" align="center">
forloop(iii,1,LIG,[    <tr>
forloop(jjj,1,COL,[        <td align="center">VECT_HEAD(_list)</td>
])    </tr>
])
</table>])


define([TABLE],[
define([COL],[$1]) dnl nombre de colonnes
define([_list],[shift($@)])
define([LIG],[eval($# / COL)]) dnl nombre de lignes
<table width="100%" align="center">
forloop(iii,1,LIG,[    <tr>
forloop(jjj,1,COL,[        <td align="center">VECT_HEAD(_list)</td>
])    </tr>
])
</table>])

define([_LISTE_],[ifelse([$1],,,[<li>$1</li> _LISTE_(shift($@))])])

define([LISTE],[
<!-- Begin list -->
<ul>_LISTE_($@)</ul>
<!-- End list -->])

define([ENUM],[
<!-- Begin enum -->
<ol>_LISTE_($@)</ol>
<!-- End enum -->])

dnl #####################################################################
dnl HTML SECTION
dnl #####################################################################
define([SECTION],[_SECTION1_(h1,$@)])
define([SUBSECTION],[_SECTION2_(h2,$@)])
define([SUBSUBSECTION],[_SECTION2_(h3,$@)])
define([CODE],[<div class="code"><pre><code>])
define([ENDCODE],[</code></pre></div>])
define([BOX],[<div class="boite">])
define([ENDBOX],[RIGHT(LINK(#gotoup,ARROW_UP)) </div>])

define([FINALBOX],[TABLEAU(4,
<h3>ifelse(LANG,fr,[Page précédente :],[Previous page:])</h3>,
<h3>ifelse(LANG,fr,[Page d'accueil :],[Home page:])</h3>,
<h3>ifelse(LANG,fr,[Aller en haut :],[Go up:])</h3>,
<h3>ifelse(LANG,fr,[Page suivante :],[Next page:])</h3>,

ifelse([$1],,[ARROW_LEFT],[ARROW_LLEFT]),
LINK(index-LANG.html,ARROW_HOME),
LINK(#gotoup,ARROW_UP),
ifelse([$3],,[ARROW_RIGHT],[ARROW_RRIGHT]),

ifelse([$1],,,[MYLINK($1,$2)]),
,,
ifelse([$3],,,[MYLINK($3,$4)]))
])

dnl ---------------------------------------------------------------------
dnl _SECTION_
dnl $1: h1 h2 h3
dnl $2: title
dnl $3: anchor
dnl $4: link
define([_THE_SECTION_],
[ifelse($4,,[ifelse($3,,[<$1>$2</$1>],[<a id="$3" name="$3"></a><$1>$2</$1>])],
[ifelse($3,,[<$1><a href="$4-]LANG[.html">$2</a></$1>],dnl
[<a id="$3" name="$3"></a><$1><a href="$4-]LANG[.html">$2</a></$1>])])])

define([_IEME_SECTION_],[0])
define([_SECTION2_],[ifelse(_IEME_SECTION_,0,,[ENDBOX])
<!-- Section -->
_THE_SECTION_($@)BOX[]define([_IEME_SECTION_],[1])])

define([_SECTION1_],[ifelse(_IEME_SECTION_,0,,[ENDBOX])
<!-- Section -->
_THE_SECTION_($@)define([_IEME_SECTION_],[0])])

dnl #####################################################################
dnl Facilites
dnl #####################################################################
define([EN_CONSTRUCTION],[ICON_BUILD ifelse(LANG,fr,[STRONG(ATTENTION
: Page en construction et projet en cours de
realisation)],[STRONG(WARNING : This site is under construction and
this project is not running yet)])])

dnl ---------------------------------------------------------------------
define([_CHAPITRAGE],[ifelse($1,,,[<li>LINK(#$2,$1)</li>
_CHAPITRAGE(shift(shift($@)))])])

define([CHAPITRAGE],[<!-- Table of contents -->
<p>STRONG(ifelse(LANG,fr,[Table des matières :],[Table of contents:]))
</p><ul>_CHAPITRAGE($@)</ul>])

dnl ---------------------------------------------------------------------
define([_RUBRIC_],
[ifelse($2,,,[<li><a href="$2"><img class="icone7" src="img/bullet.gif" alt=""/>$1.</a></li>
_RUBRIC_(shift(shift($@)))])])

define([_RUBRICHTML_],
[ifelse($2,,,$2,THISFILE,[<li id="_ACTIF_">$1</li> _RUBRICHTML_(shift(shift($@)))],
[<li><a href="$2-]LANG[.html">$1</a></li>
_RUBRICHTML_(shift(shift($@)))])])

dnl LEFT_RUBRIC(titre, rubrique1, lien1, rubrique2, lien2, ...)
define([LEFT_RUBRIC],[<!-- Left Rubric -->
ifelse($1,,[],[<h1>$1 :</h1>])
<ul class="items">
_RUBRIC_(shift($@))
</ul>])

define([_all],[ifelse([$1],,,[$1 _all(shift($@))])])

dnl LEFT_MENU(LEFT_RUBRIC(...), ...)
define([LEFT_MENU],[
<!-- Left menu -->
<div id="menu">
_all($@)
dnl <img class="logo" src="img/THISFILE.gif" alt=""/>
</div>])

dnl ---------------------------------------------------------------------
define([HEADER_MENU],[
<div id="$1">
<ul class="$2">
define([_ACTIF_],[$3])
_RUBRICHTML_(shift(shift(shift($@))))
undefine([_ACTIF_])
</ul>
</div>])

define([HEADER_MENU1],[<!-- Memu Haut 1 --> HEADER_MENU(header2,rubriques2,actif2,$@)])
define([HEADER_MENU2],[<!-- Memu Haut 2 --> HEADER_MENU(header3,rubriques3,actif3,$@)])

dnl #####################################################################
dnl Mise en forme
dnl #####################################################################
define([STRONG],[<strong>$1</strong>])
define([BOLD],[STRONG])
define([MATHS],[<strong>$1</strong>])
define([ITALIQUE],[<em>$1</em>])
define([REMARQUE],[<strong>Remarque :</strong>])
define([CENTER],[<div class="centrer">$@</div>])
define([RIGHT],[<div class="right">$@</div>])
define([HUDGE],[<div class="hudge">$@</div>])

dnl #####################################################################
dnl Icones
dnl #####################################################################
define([DRAP_FR],[<img src="img/fr.png" style="border:0;" alt="fr"/>])
define([DRAP_EN],[<img src="img/en.png" style="border:0;" alt="en"/>])
define([ICON_PDF],[<img class="icone30" src="img/pdf.png" alt=""/>])
define([ICON_TGZ],[<img class="icone30" src="img/tgz.png" alt=""/>])
define([ICON_BUILD],[<img class="icone30" src="img/construction2.gif" alt="# EN CONSTRUCTION #"/>])
define([ICON_DOWNLOAD],[<img class="icone20" src="img/tgz.png" alt=""/>])
define([ICON_READ],[<img class="icone20" src="img/read.png" alt=""/>])
define([ICON_EYES],[<img class="icone20" src="img/eyes.png" alt=""/>])
define([ICON_GEAR],[<img class="icone20" src="img/gear.png" alt=""/>])
define([ICON_HOME],[<img class="icone20" src="img/home.png" alt=""/>])
define([ICON_H2G2],[<img class="icone20" src="img/h2g2.png" alt=""/>])
define([ICON_NEW],[<img class="icone30" src="img/new.gif" alt="new"/>])
define([ICON_UPDATE],[<img src="img/updated.gif" alt="renoved"/>])

dnl ---------------------------------------------------------------------

define([ARROW_HOME],[<img class="icone5" src="img/home.png" alt="home"/>])
define([ARROW_UP],[<img class="icone5" src="img/2uparrow.png" alt="up"/>])
define([ARROW_DOWN],[<img style="border:0" src="bimg/as.gif" alt="down"/>])
define([ARROW_LEFT],[<img class="icone5" src="img/leftarrowgris.png" alt="Prev."/>])
define([ARROW_LLEFT],[<img class="icone5" src="img/2leftarrow.png" alt="ext Prev."/>])
define([ARROW_RIGHT],[<img class="icone5" src="img/rightarrowgris.png" alt="Next"/>])
define([ARROW_RRIGHT],[<img class="icone5" src="img/2rightarrow.png" alt="Next"/>])

dnl ---------------------------------------------------------------------
define([ITEM_HOME],[ICON_HOME[]ifelse(LANG,fr,[Accueil],[Home])])
define([ITEM_EMBARQUE],[ICON_GEAR[]ifelse(LANG,fr,[Systèmes embarqués],[Embedded systems])])
define([ITEM_3D],[ICON_EYES[]ifelse(LANG,fr,[Projets informatiques],[3D])])
define([ITEM_CV],[ICON_READ[]ifelse(LANG,fr,[Stages &amp; CV],[Internships &amp; CV])])
define([ITEM_TUTO],[ICON_READ[]ifelse(LANG,fr,[Tutoriels],[Tutorials])])

define([MENU_DU_HAUT],[HEADER_MENU1(ITEM_HOME,index,ITEM_EMBARQUE,helico_intro,ITEM_3D,ecstasy,ITEM_TUTO,tuto_scicos)])
define([MENU_HOME],[HEADER_MENU2(Home,index,Plan du site,plansite,Mises à jour,maj,C.V.,cv)])
define([MENU_EMBARQUE],[HEADER_MENU2(Hélicoptère 4 rotors,helico_intro,Voiture CyCab,stages_travail2)])
define([MENU_3D],[HEADER_MENU2(Ecstasy,ecstasy,SpeedWay,speedway,SimTaDyn,simtadyn,Interpréteur Forth,myforth)])
define([MENU_TUTO],[HEADER_MENU2(Logiciels Scilab/Scicos,tuto_scicos,Logiciel SynDEx,tuto_syndex,Port série,comm_rs232,BSP,tuto_bsp,ASE,tuto_ase,Dynamique d'une voiture,tuto_dyna,)])
define([MENU_CV],[HEADER_MENU2(Mon C.V.,cv,Mes stages à l'INRIA,stages_travail2)])

dnl #####################################################################
dnl Banieres, Logos, Licences
dnl #####################################################################
define([BANNIERES],[<hr/><!-- Bannieres menu -->
LICENCE_TEXT LICENCE_CODE <table width="100%" align="center">
<tr><td rowspan="3">LICENCE_LOGO</td><td>XHTML_VALIDATOR</td></tr>
<tr><td>CSS_VALIDATOR</td></tr>
<tr><td>PHPMYVISITE</td></tr>
</table>PRINT_MAJ<hr/>])

define([CSS_VALIDATOR],[
<a href="http://jigsaw.w3.org/css-validator/check/referer">
<img src="img/css.gif" alt="Valid CSS!" style="border:0" height="15" width="66" /></a>])


define([XHTML_VALIDATOR],[<a href="http://validator.w3.org/check?uri=referer">
<img src="http://www.w3.org/Icons/valid-xhtml10" alt="Valid XHTML 1.0!"
style="border:0" height="15" width="66" /></a>])

dnl ---------------------------------------------------------------------
define([LICENCE_TEXT],[ifelse(LANG,fr,
[Les documents (PDF, codes sources, programmes, ...) fournis sur ce site sont mis à disposition sous licence ],
[The documents (PDF, programs, ...) on this site are licensed under the ])
<a href="http://creativecommons.org/licenses/GPL/2.0/">CC-GNU GPL</a>.])

dnl ---------------------------------------------------------------------
define([LICENCE_LOGO],[<!-- Creative Commons License -->
<a href="http://creativecommons.org/licenses/GPL/2.0/">
<img alt="CC-GNU GPL" style="border:0;" src="http://creativecommons.org/images
/public/cc-GPL-a.png" /></a>
<!-- /Creative Commons License -->])

define([LICENCE_CODE],[<!--
<rdf:RDF xmlns="http://web.resource.org/cc/"
    xmlns:dc="http://purl.org/dc/elements/1.1/"
    xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#">
<Work rdf:about="">
   <license rdf:resource="http://creativecommons.org/licenses/GPL/2.0/" />
   <dc:type rdf:resource="http://purl.org/dc/dcmitype/Software" />
</Work>

<License rdf:about="http://creativecommons.org/licenses/GPL/2.0/">
<permits rdf:resource="http://web.resource.org/cc/Reproduction" />
   <permits rdf:resource="http://web.resource.org/cc/Distribution" />
   <requires rdf:resource="http://web.resource.org/cc/Notice" />
   <permits rdf:resource="http://web.resource.org/cc/DerivativeWorks" />
   <requires rdf:resource="http://web.resource.org/cc/ShareAlike" />
   <requires rdf:resource="http://web.resource.org/cc/SourceCode" />
</License>
</rdf:RDF> -->])

dnl ---------------------------------------------------------------------
define([PHPMYVISITE],[<!-- phpmyvisites -->
<a href="http://st.free.fr/" title="phpMyVisites | Open source web analytics"
onclick="window.open(this.href);return(false);"><script type="text/javascript">
<!--
var a_vars = Array();
var pagename='';

var phpmyvisitesSite = 21477;
var phpmyvisitesURL = "http://st.free.fr/phpmyvisites.php";
//-->
</script>
<script language="javascript" src="http://st.free.fr/phpmyvisites.js" type="text/javascript"></script>
<object><noscript>
<img src="img/phpmyvisites.png" alt="Statistics" style="border:0" />
</noscript></object></a>
<!-- /phpmyvisites -->])



define([END_BODY],[
divert
_HTML_HEADER_
<table id="page">
  <!-- SKELETON BEGIN HEADER -->
  <tr>
    <td colspan="2">
      <div id="header1">
	<!-- BEGIN USER HEADER 1 -->
	_META_TITLE_
	<!-- END USER HEADER 1 -->
	</div>
    </td>
  </tr>

  <tr>
    <td colspan="2">
      MENU_DU_HAUT
    </td>
  </tr>

  <tr>
    <td colspan="2">
      _SSMENU_
    </td>
  </tr>
  <!-- SKELETON END HEADER -->

  <!-- SKELETON BEGIN NAVIGATION MENU -->
  <tr>
    <td id="navigation">
	<!-- BEGIN USER NAVIGATION MENU -->
	undivert(DIVERT_NAVIGATION_CODE)
	<!-- END USER NAVIGATION MENU -->
    </td>
    <!-- SKELETON END NAVIGATION MENU -->


    <!-- SKELETON BEGIN BODY -->
    <td id="content">
	<!-- BEGIN USER BODY -->

	undivert(DIVERT_BODY_CODE)
        </div>
	<!-- END USER BODY -->
    </td>
  </tr>
  <!-- SKELETON END BODY -->

  <!-- SKELETON BEGIN FOOTER -->
  <tr>
    <td colspan="2" id="footer">
	  <!-- BEGIN USER FOOTER -->
	  undivert(DIVERT_FOOTER_CODE)
          FINALBOX($@)
	  <!-- END USER FOOTER -->
    </td>
  </tr>
  <!-- SKELETON END FOOTER -->
</table>
HTML_FOOTER
<!-- EOF -->])
