dnl -*- m4 -*- Coloration Syntaxique pour Emacs
dnl #####################################################################
dnl Head menu.
dnl The head menu is composed of two horizontal menus: the primary and
dnl the secondary. The primary showdisplays the website categories while
dnl the secondary displays differents articles.
dnl #####################################################################
dnl Helper macros for building head menu (primary and secondary)
define([display_all_menu_elements_],[ifelse($2,,,$2,THISFILE,[<li id="_which_menu_item_active_">$1</li>
display_all_menu_elements_(shift(shift($@)))],
[<li><a href="$2-]LANG[.html">$1</a></li>
display_all_menu_elements_(shift(shift($@)))])])dnl
define([HEAD_MENU],[<div id="$1">
<ul class="$2">
define([_which_menu_item_active_],[$3])dnl
display_all_menu_elements_(shift(shift(shift($@))))dnl
undefine([_which_menu_item_active_])dnl
</ul>
</div>])dnl
dnl ---------------------------------------------------------------------
define([primary_head_menu],[<!-- Begin Menu -->
HEAD_MENU(primary_header,rubriques2,actif2,$@)
<!-- End Menu -->])dnl
define([secondary_head_menu],[<!-- Begin Menu -->
HEAD_MENU(secondary_header,rubriques3,actif3,$@)
<!-- End Menu -->])dnl
dnl ---------------------------------------------------------------------
dnl Define head menu items
define([ITEM_HOME],[ICON_HOME[]TRANSLATE([Accueil],[Home])])
define([ITEM_EMBARQUE],[ICON_GEAR[]TRANSLATE([Systèmes embarqués],[Embedded systems])])
define([ITEM_3D],[ICON_EYES[]TRANSLATE([Projets informatiques],[3D])])
define([ITEM_CV],[ICON_READ[]TRANSLATE([Stages &amp; CV],[Internships &amp; CV])])
define([ITEM_TUTO],[ICON_READ[]TRANSLATE([Tutoriels],[Tutorials])])
dnl ---------------------------------------------------------------------
dnl Define different menus
define([PRIMARY_HEAD_MENU_1],[primary_head_menu(ITEM_EMBARQUE,helico_intro,ITEM_TUTO,dyna,ITEM_3D,simtadyn,ITEM_HOME,index)])
define([SECONDARY_HEAD_MENU_HOME],[secondary_head_menu(Home,index,Plan du site,plansite,Mises à jour,maj,C.V.,cv)])
define([SECONDARY_HEAD_MENU_EMBARQUE],[secondary_head_menu(Voiture CyCab,stages_travail2,Hélicoptère 4 rotors,helico_intro)])
define([SECONDARY_HEAD_MENU_3D],[secondary_head_menu(Chess,chessneuneu,Ecstasy,ecstasy,SpeedWay,speedway,SimTaDyn,simtadyn)])
define([SECONDARY_HEAD_MENU_TUTO],[secondary_head_menu(BSP,bsp,SynDEX,syndex,Boîte à outils Max+,maxplus,Interpréteur Forth,forth,Dynamique d'une voiture,dyna)])
define([SECONDARY_HEAD_MENU_CV],[secondary_head_menu(Mon C.V.,cv,Mes stages à l'INRIA,stages_travail2)])
dnl ---------------------------------------------------------------------
dnl Select the desired menu with this macro
define([SECONDARY_HEAD_MENU],[define([_SECONDARY_HEAD_MENU_],[$1])])dnl
define([PRIMARY_HEAD_MENU],[define([_PRIMARY_HEAD_MENU_],[$1])])dnl
PRIMARY_HEAD_MENU(PRIMARY_HEAD_MENU_1)dnl Ok there is no selection possible for the moment
dnl #####################################################################
dnl Left menu.
dnl The left menu is a set of rubriques. A rubrique is a list of items.
dnl An example of rubrique is the table of contents where items are links
dnl to sections of the document.
dnl #####################################################################
dnl ---------------------------------------------------------------------
define([ITEM_BULLET],[ifelse($1,,[],[<li><a href="$2"><img class="icone7" src="img/bullet.gif" alt=""/>$1.</a></li>])])
define([display_all_items_],[ifelse($2,,,[dnl Do not include item self referencing itself
ifelse($2,THISFILE-LANG.html,[<!--],[]) ITEM_BULLET($1,$2) ifelse($2,THISFILE-LANG.html,[-->],[])
display_all_items_(shift(shift($@)))])])dnl
dnl ---------------------------------------------------------------------
define([HEADING],[<!-- Left Heading -->
ifelse($1,,[],[<h1>$1 :</h1>])
<ul class="items">
display_all_items_(shift($@))dnl
</ul>
])
dnl #####################################################################
dnl Left Heading: is a box with links located on the left of the page
dnl #####################################################################
define([LEFT_BORDER],[divert(DIVERT_LEFT_BORDER)$@])
dnl #####################################################################
dnl Left heading specific for the page
dnl #####################################################################
define([HEADING_3D_GAME],[HEADING(TRANS_SAME_PROJ,Speedway,speedway-LANG.html,Ecstasy,ecstasy-LANG.html)])
define([HEADING_SIMATDYN_LIKE],[HEADING(TRANS_SAME_PROJ,Spreadsheet-based GIS Models,dnl
[https://scholarworks.umb.edu/cgi/viewcontent.cgi?referer=&httpsredir=1&article=1004&context=management_wp],dnl
SimCity engine,[http://www.andrewwillmott.com/talks/inside-glassbox],dnl
KDE Rocs,https://github.com/KDE/rocs)])dnl
dnl #####################################################################
dnl HTML template: the page is a html table
dnl -------------------------------------
dnl            Title
dnl -------------------------------------
dnl        Primary header
dnl -------------------------------------
dnl        Secondary header
dnl -------------------------------------
dnl Left border | Article | Right border
dnl -------------------------------------
dnl            Footer
dnl #####################################################################
define([BEGIN_BODY],[divert(DIVERT_BODY)])
define([END_BODY],[divert(DIVERT_FOOTER)
divert
HTML_HEADER
<table id="document">

  <!-- DOCUMENT HEADER -->

  <!-- Page Title -->
  <tr>
    <td colspan="2">
      <div id="main_title">
        _META_TITLE_
      </div>
    </td>
    <td id="right_top_border"></td>
  </tr>

  <!-- Primary Menu -->
  <tr>
    <td colspan="2">
      _PRIMARY_HEAD_MENU_
    </td>
    <td id="right_border"> <!-- empty --> </td>
  </tr>

  <!-- Secondary Menu -->
  <tr>
    <td colspan="2">
      _SECONDARY_HEAD_MENU_
    </td>
    <td id="right_border"> <!-- empty --> </td>
  </tr>

  <!-- DOCUMENT BODY -->
  <tr>
    <!-- BEGIN LEFT BORDER -->
    <td id="left_border">
        <div id="menu">
           <!-- Translated Pages -->
           CHANGE_LANGUAGE

           <!-- BEGIN LEFT BORDER DIVERSION -->
           undivert(DIVERT_LEFT_BORDER)
           <!-- END LEFT BORDER DIVERSION -->

<br/><hr/>BADGES
PROJ_WEBGENERATOR(ICON_GITHUB2)
HEADING(TRANS_UPDATED,DATE,https://github.com/Lecrapouille/WebSiteGenerator)
<hr/>
        </div>
    </td>
    <!-- END LEFT BORDER -->

    <!-- BEGIN ARTICLE -->
    <td id="article">
        undivert(DIVERT_BODY)
        </div>
    </td>
    <!-- END ARTICLE BODY -->

    <!-- BEGIN RIGHT BORDER -->
    <td id="right_border"> <!-- empty --> </td>
    <!-- END RIGHT BORDER -->
  </tr>

  <!-- DOCUMENT FOOTER -->
  <tr>
    <td colspan="3" id="footer">
          <!-- BEGIN USER FOOTER -->
          undivert(DIVERT_FOOTER)
          PREV_NEXT_ARTICLE($@)
          <!-- END USER FOOTER -->
    </td>
  </tr>
</table>
HTML_FOOTER

<!-- This page was generated by script DATE -->])dnl
